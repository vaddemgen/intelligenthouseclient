import com.github.spotbugs.snom.SpotBugsTask

plugins {
  id 'java'
  id 'com.github.spotbugs' version '4.0.1'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
  id 'pmd'
}

apply from: rootProject.file('gradle/checkstyle.gradle')
apply from: rootProject.file('gradle/spotbugs-all-task.gradle')
apply from: rootProject.file('gradle/install-git-hooks.gradle')

group 'com.vaddemgen'
version '1.0.0-alpha.3'

sourceCompatibility = 11

ext {
  // https://mvnrepository.com/artifact/com.pi4j/pi4j-core
  pi4jVersion = '1+'
  // https://mvnrepository.com/artifact/org.projectlombok/lombok
  lombokVersion = '1+'
  // https://mvnrepository.com/artifact/com.google.code.gson/gson
  gsonVersion = '2+'
  // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
  apacheCommonsLang3Version = '3+'
  // https://mvnrepository.com/artifact/com.google.dagger/dagger
  daggerVersion = '2.50'
  // https://mvnrepository.com/artifact/org.spockframework/spock-bom
  spockBomVersion = '2.0-M2-groovy-3.0'
  // https://mvnrepository.com/artifact/org.codehaus.groovy/groovy-bom
  groovyBomVersion = '3.0.2'
}

/* SpotBugs */
tasks.withType(SpotBugsTask) {
  reports {
    html.enabled true
  }
}
spotbugs {
  excludeFilter.set(file('config/findbugs/excludeFilter.xml'))
}
/* ./SpotBugs */

/* PMD */
tasks.withType(Pmd) {
  reports {
    xml.enabled false
  }
}
/* ./PMD */

jar {
  manifest {
    attributes(
        'Main-Class': 'com.vaddemgen.intelligenthouseclient.Application'
    )
  }
  from {
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation platform(project(':platform'))

  implementation "com.pi4j:pi4j-core:$pi4jVersion"
  implementation "com.google.code.gson:gson:$gsonVersion"
  implementation "org.apache.commons:commons-lang3:$apacheCommonsLang3Version"

  // https://mvnrepository.com/artifact/org.apache.kafka/kafka-clients
  implementation 'org.apache.kafka:kafka-clients:3.3.1'

  testImplementation 'org.junit.jupiter:junit-jupiter-params'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'

  implementation "com.google.dagger:dagger:$daggerVersion"

  // Spock
  testImplementation platform("org.spockframework:spock-bom:$spockBomVersion")
  testImplementation 'org.spockframework:spock-core'

  testImplementation platform("org.codehaus.groovy:groovy-bom:$groovyBomVersion")

  annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion",
      "org.projectlombok:lombok:$lombokVersion"
}
